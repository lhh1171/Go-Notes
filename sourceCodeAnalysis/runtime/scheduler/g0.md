# Main goroutine调度

Go入口main函数也是一个Goroutine,也就是G0。G0如何调度？

## 程序入口

### 实例代码

```
func main() {
	println("Test...")
}
```

#### 入口函数

```
$ go build -gcflags "-N -l -m" -o test main.go

// 入口地址为0x44a4c0
$ readelf -h test
    ELF 头：
    Magic：  7f 45 4c 46 02 01 01 00 00 00 00 00 00 00 00 00
    Class:                             ELF64
    Data:                              2's complement, little endian
    Version:                           1 (current)
    OS/ABI:                            UNIX - System V
    ABI Version:                       0
    Type:                              EXEC (可执行文件)
    Machine:                           Advanced Micro Devices X86-64
    Version:                           0x1
    入口点地址：              0x44a4c0
    程序头起点：              64 (bytes into file)
    Start of section headers:          456 (bytes into file)
    标志：             0x0
    本头的大小：       64 (字节)
    程序头大小：       56 (字节)
    Number of program headers:         7
    节头大小：         64 (字节)
    节头数量：         24
    字符串表索引节头： 3
  
// 通过符号表,查看程序入口函数
$ objdump -d -M intel test | less 
000000000044a4c0 <_rt0_amd64_linux>:
  44a4c0:       e9 db c6 ff ff          jmp    446ba0 <_rt0_amd64>
  44a4c5:       cc                      int3
  44a4c6:       cc                      int3
  44a4c7:       cc                      int3
  44a4c8:       cc                      int3
  44a4c9:       cc                      int3
  44a4ca:       cc                      int3
  44a4cb:       cc                      int3
  44a4cc:       cc                      int3
  44a4cd:       cc                      int3
  44a4ce:       cc                      int3
  44a4cf:       cc                      int3

// runtime/asm_amd64.s
// _rt0_amd64_linux 跳转到_rt0_amd64函数
TEXT _rt0_amd64(SB),NOSPLIT,$-8
	MOVQ    0(SP), DI   // argc
	LEAQ    8(SP), SI   // argv
	JMP runtime·rt0_go(SB)
	
// runtime/asm_amd64.s
// _rt0_amd64函数跳转到rt0_go函数,所有的初始化及线程启动都是在该函数完成
```


```
TEXT runtime·rt0_go(SB),NOSPLIT,$0
        // 1. 命令行参数值分别放到AX,BX寄存器中
        // 2. 自动调整栈指针
        // 3. 按16比特内存对齐
        // 4. 将参数放入stack中
        MOVQ    DI, AX                                      // argc,参数个数
        MOVQ    SI, BX                                      // argv,参数数组指针
        SUBQ    $(4*8+7), SP                                // 自动调整sp指针
        ANDQ    $~15, SP                                    // 内存对齐
        MOVQ    AX, 16(SP)                                  // 将参数放会栈中
        MOVQ    BX, 24(SP)
				
        // 1. 全局变量runtime.g0,放入DI寄存器(runtime.g0为main goroutine)
        // 2. 检查是否需要扩容stack空间(g_stackguard0, g_stackguard1)
        // 3. 设置goroutine 栈底(bp)、栈顶(sp)
        MOVQ    $runtime·g0(SB), DI                         // 全局变量g0,放入寄存器di;runtime.g0为main goroutine
        LEAQ    (-64*1024+104)(SP), BX	                    
        MOVQ    BX, g_stackguard0(DI)                       // g_stackguard0,用于栈增长 
        MOVQ    BX, g_stackguard1(DI)	                    // g_stackguard1,用于栈增长(cgo) 
        MOVQ    BX, (g_stack+stack_lo)(DI)                  // goroutine bp
        MOVQ    SP, (g_stack+stack_hi)(DI)                  // goroutine sp
				
	// 设置TLS(Thread Local Storage)
	// 将m0变量地址存储在DI寄存器,然后调用runtime.settls函数,TLS指向m0变量.
    needtls:
        LEAQ    runtime·m0+m_tls(SB), DI
        CALL    runtime·settls(SB)

        // 通过TLS进行存储,保证TLS工作原理 
        get_tls(BX)									 

        // m0地址加载到BX寄存器
        MOVQ    $0x123, g(BX)                               // mov    QWORD PTR gs:0x30,0x123, 将0x123值保存在[m0+0x30]位置
        MOVQ    runtime·m0+m_tls(SB), AX                    // 确保[GS+0x30]和[rip+0x7297d]指向同一位置
        CMPQ    AX, $0x123
        JEQ 2(PC)
		CALL    runtime·abort(SB)
				
    ok:
		get_tls(BX)                                 // m0地址加载到BX寄存器
        LEAQ    runtime·g0(SB), CX
		MOVQ    CX, g(BX)
        LEAQ    runtime·m0(SB), AX

        // 将runtime.g0变量的地址保存到TLS中
        // save m->g0 = g0
        MOVQ    CX, m_g0(AX)
        		
        // 初始化runtime.m0变量
        // save m0 to g0->m
        MOVQ    AX, g_m(CX)
        
        CLD                                                 // 清除FLAGS寄存器方向标志
        CALL    runtime·check(SB)                           // 内置类型检查,检查他们的大小及其它参数等,如果出了什么错,会产生panic错误
        		
        // runtime.args负责处理以下参数向量,runtime只care startup RandomData,它主要用来初始化hash函数以及指向系统调用位置的指针.
        // 初始化了以下变量:
        // 1. __vdso_time_sym
        // 2. __vdso_gettimeofday_sym
        // 3. __vdso_clock_gettime_sym
        MOVL    16(SP), AX                                  // copy argc
        MOVL    AX, 0(SP)
        MOVQ    24(SP), AX                                  // copy argv
        MOVQ    AX, 8(SP)
        CALL    runtime·args(SB)
        		
        // 初始化cpu, ncpu保存了当前cpu数量
        CALL    runtime·osinit(SB)
           	
        // go调度器初始化
        // 1. 获得当前goroutine指针,该指针指向一个g结构体
        // 2. 初始化runtime.raceinit
        // 3. 初始化其它一些函数
        CALL    runtime·schedinit(SB)
           	
        // 创建Main goroutine 启动程序
        MOVQ    $runtime·mainPC(SB), AX                     // entry
        PUSHQ   AX									  
        
        // m压入栈
        PUSHQ   $0                                          // arg size
        
        
        CALL    runtime·newproc(SB)                         // 创建新的goroutine运行函数fn.放入g等待队列,等待被调度
        POPQ    AX
        POPQ    AX
        
        // start this M
        CALL    runtime·mstart(SB)                          // 启动线程
        
        CALL    runtime·abort(SB)                           // 用于异常检查,该代码不会被执行
        RET
        RET
```

### TLS
```
TEXT runtime·settls(SB),NOSPLIT,$32
    	ADDQ	$8, DI                                      // 指向m0线程栈栈底(FS)

    	MOVQ	DI, SI           
    	MOVQ	$0x1002, DI                                 // ARCH_SET_FS,设置FS寄存器转成64位基址
    	MOVQ	$SYS_arch_prctl, AX                         // 设置线程状态
    	SYSCALL                                             // 系统调用
```


#### FS寄存器
FS寄存器指向当前堪称的TEB结构(线程结构)。

```
偏移量    具体含义
000      指向SEH链指针
004      线程堆栈顶部
008      线程堆栈底部
00C      SubSystemTib
010      FiberData
014      ArbitraryUserPointer
018      FS段寄存器在内存中的镜像地址
020      进程PID
024      线程ID
02C      指向线程局部存储指针
030      PEB结构地址（进程结构）
034      上个错误号
```


### 系统初始化

```
// 系统初始化
func osinit() {
	// 获取CPU逻辑核心
	ncpu = getproccount()
	
	// PhysHugePageSize 是操作系统默认物理大页字节大小,分配对程序不透明
	// 读取操作系统透明大页: /sys/kernel/mm/transparent_hugepage/hpage_pmd_size 
    physHugePageSize = getHugePageSize()
}
```

#### CPU数量
ncpu == P数量

```
func getproccount() int32 {
	const maxCPUs = 64 * 1024
	var buf [maxCPUs / 8]byte
	r := sched_getaffinity(0, unsafe.Sizeof(buf), &buf[0])
	if r < 0 {
		return 1
	}
	n := int32(0)
	for _, v := range buf[:r] {
		for v != 0 {
			n += int32(v & 1)
			v >>= 1
		}
	}
	if n == 0 {
		n = 1
	}
	return n
}


// 获取CPU Cores
// Systemcall
TEXT runtime·sched_getaffinity(SB),NOSPLIT,$0
	MOVQ	pid+0(FP), DI
	MOVQ	len+8(FP), SI
	MOVQ	buf+16(FP), DX
	MOVL	$SYS_sched_getaffinity, AX
	SYSCALL
	MOVL	AX, ret+24(FP)
	RE
```

#### 内存页大小初始化
Go内存页大小和操作系统默认的透明大页相同。

```
var sysTHPSizePath = []byte("/sys/kernel/mm/transparent_hugepage/hpage_pmd_size\x00")

// 透明大页大小
func getHugePageSize() uintptr {
	var numbuf [20]byte
	fd := open(&sysTHPSizePath[0], 0 /* O_RDONLY */, 0)
	if fd < 0 {
		return 0
	}
	n := read(fd, noescape(unsafe.Pointer(&numbuf[0])), int32(len(numbuf)))
	closefd(fd)
	if n <= 0 {
		return 0
	}
	l := n - 1 // remove trailing newline
	v, ok := atoi(slicebytetostringtmp(numbuf[:l]))
	if !ok || v < 0 {
		v = 0
	}
	if v&(v-1) != 0 {
		// v is not a power of 2
		return 0
	}
	return uintptr(v)
}
```

